buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

node {
    version = '11.3.0'
    yarnVersion = 'v1.12.3'
    download = true
}

yarn_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.files 'package.json'
    inputs.files 'yarn.lock'

    outputs.dir 'build'
}

task packageGraphQLPlaygroundUI(type: Zip) {
    dependsOn yarn_build
    baseName 'graphql-playground-ui'
    extension 'jar'
    destinationDir file("${projectDir}/build_packageGraphQLPlaygroundUI")
    from('build') {
        into 'static'
    }
}

configurations {
    yarnResources
}

configurations.default.extendsFrom(configurations.yarnResources)

artifacts {
    yarnResources(packageGraphQLPlaygroundUI.archivePath) {
        builtBy packageGraphQLPlaygroundUI
        type "jar"
    }
}

assemble.dependsOn packageGraphQLPlaygroundUI

String testsExecuteMarkerName = "${projectDir}/.tests.executed"

task test(type: YarnTask) {
    dependsOn assemble
    environment CI:'true'
    args = ['test']

    inputs.files fileTree('src')
    inputs.files 'package.json'
    inputs.files 'yarn.lock'

    doLast{
        new File(testsExecuteMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }

    outputs.file testsExecuteMarkerName
}

check.dependsOn(test)

clean {
    delete packageGraphQLPlaygroundUI.archivePath
    delete testsExecuteMarkerName
}